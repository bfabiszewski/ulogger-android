/*
 * Copyright (c) 2023 Bartek Fabiszewski
 * http://www.fabiszewski.net
 *
 * This file is part of Î¼logger-android.
 * Licensed under GPL, either version 3, or any later.
 * See <http://www.gnu.org/licenses/>
 */

apply plugin: 'com.android.application'

android {
    compileSdk 35

    defaultConfig {
        applicationId 'net.fabiszewski.ulogger'
        minSdkVersion 21
        targetSdkVersion 35
        versionCode 311
        versionName '3.11'
        resConfigs "en", "ca", "cs", "de", "el", "es", "eu", "fi", "fr", "gl", "it", "ja", "pl", "pt-rBR", "ru", "sk", "zh-rCN"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-dev'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    lint {
        disable 'GoogleAppIndexingWarning', 'MissingTranslation', 'NotificationIconCompatibility'
    }
    namespace 'net.fabiszewski.ulogger'
    buildFeatures {
        buildConfig true
    }
}

configurations.configureEach {
    resolutionStrategy {
        eachDependency {
            // fix duplicate class in kotlin-stdlib dependencies
            if ((requested.group == "org.jetbrains.kotlin") && (requested.name.startsWith("kotlin-stdlib"))) {
                useVersion("1.8.0")
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'androidx.exifinterface:exifinterface:1.4.0'
    testImplementation 'junit:junit:4.13.2'
}
